buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}

plugins {
    id 'groovy'
    id 'application'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

project.ext {
    configPath = "$rootProject.projectDir/config/"

    git = org.ajoberstar.grgit.Grgit.open(file('.'))
}

String specifiedVersion = '0.1.0'
version = "$specifiedVersion.${git.head().abbreviatedId}"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.9.1'
    compile 'io.dropwizard:dropwizard-db:0.9.1'
    compile 'io.dropwizard:dropwizard-jdbi:0.9.1'
    compile 'io.dropwizard.modules:dropwizard-flyway:0.9.0-1'
    compile 'io.dropwizard.modules:dropwizard-java8:0.9.0-1'
    compile 'io.dropwizard.modules:dropwizard-java8-jdbi:0.9.0-1'
    compile 'org.codehaus.groovy:groovy-all:2.4.5'

    testCompile 'cglib:cglib-nodep:3.2.0'
    testCompile 'io.dropwizard:dropwizard-client:0.9.1'
    testCompile 'io.dropwizard:dropwizard-testing:0.9.1'
    testCompile 'org.objenesis:objenesis:2.2'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    runtime 'com.h2database:h2:1.4.190'
}

mainClassName = "com.playmore.exerciselog.ExerciseLogApplication"

jar {
    manifest {
        attributes("Implementation-Title": "Gradle", "Implementation-Version": version)
    }
}

run {
    args 'server', configPath + 'app_config.yml'
}